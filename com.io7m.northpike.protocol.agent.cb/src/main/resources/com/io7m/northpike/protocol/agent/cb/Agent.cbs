;
; Copyright Â© 2023 Mark Raynsford <code@io7m.com> https://www.io7m.com
;
; Permission to use, copy, modify, and/or distribute this software for any
; purpose with or without fee is hereby granted, provided that the above
; copyright notice and this permission notice appear in all copies.
;
; THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
; WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
; MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY
; SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
; WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
; ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF OR
; IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
;

[language cedarbridge 1 0]

[package com.io7m.northpike.protocol.agent.cb]

[import com.io7m.cedarbridge cb]
[import com.io7m.cedarbridge.time ct]

[record NPA1StackTraceElement
  [field classLoader cb:String]
  [field moduleName cb:String]
  [field moduleVersion cb:String]
  [field declaringClass cb:String]
  [field methodName cb:String]
  [field fileName cb:String]
  [field lineNumber cb:IntegerUnsigned32]
]

[record NPA1Exception
  [field className cb:String]
  [field message cb:String]
  [field attributes [cb:Map cb:String cb:String]]
  [field cause [cb:Option NPA1Exception]]
  [field suppressed [cb:List NPA1Exception]]
  [field stackTrace [cb:List NPA1StackTraceElement]]
]

[documentation NPA1PublicKey "A public key."]
[variant NPA1PublicKey
  [documentation Ed448 "An Ed448 key."]
  [case Ed448
    [documentation value "The key data."]
    [field value cb:ByteArray]
  ]
]

[documentation NPA1LoginChallenge "A login challenge."]
[record NPA1LoginChallenge
  [documentation id "The challenge ID."]
  [field id cb:UUID]
  [documentation value "The challenge data."]
  [field value cb:ByteArray]
]

[documentation NPA1LoginChallengeCompletion "A login challenge completion."]
[record NPA1LoginChallengeCompletion
  [documentation id "The challenge ID."]
  [field id cb:UUID]
  [documentation signature "The signature data."]
  [field signature cb:ByteArray]
]

[documentation NPA1WorkItemIdentifier "A work item identifier."]
[record NPA1WorkItemIdentifier
  [documentation executionId "The execution ID."]
  [field executionId cb:UUID]
  [documentation planElementName "The plan element name."]
  [field planElementName cb:String]
]

[documentation NPA1Version "A version number."]
[record NPA1Version
  [field major cb:IntegerUnsigned32]
  [field minor cb:IntegerUnsigned32]
  [field patch cb:IntegerUnsigned32]
  [field qualifier [cb:Option cb:String]]
]

[documentation NPA1ToolReference "A tool reference."]
[record NPA1ToolReference
  [documentation referenceName "The reference name."]
  [field referenceName cb:String]
  [documentation toolName "The tool name."]
  [field toolName cb:String]
  [documentation version "The tool version."]
  [field version NPA1Version]
]

[documentation NPA1ToolExecutionEvaluated "A fully evaluated tool execution."]
[record NPA1ToolExecutionEvaluated
  [documentation toolReference "The tool reference."]
  [field toolReference NPA1ToolReference]
  [documentation environment "The environment variables."]
  [field environment [cb:Map cb:String cb:String]]
  [documentation arguments "The command arguments."]
  [field arguments [cb:List cb:String]]
]

[documentation NPA1FailurePolicy "A failure policy."]
[variant NPA1FailurePolicy
  [case Ignore]
  [case Fail]
  [case Retry [field retryCount cb:IntegerUnsigned32]]
]

[documentation NPA1CleanPolicy "A workspace clean policy."]
[variant NPA1CleanPolicy
  [case CleanLater]
  [case CleanImmediately]
]

[documentation NPA1ArchiveLinks "A link to an archive as input to a work item."]
[record NPA1ArchiveLinks
  [documentation fileURI "The file URI"]
  [field fileURI cb:URI]
  [documentation checksumURI "The checksum URI"]
  [field checksumURI cb:URI]
]

[documentation NPA1AgentWorkItem "A work item."]
[record NPA1AgentWorkItem
  [documentation identifier "The work item identifier"]
  [field identifier NPA1WorkItemIdentifier]
  [documentation metadata "The work item metadata"]
  [field metadata [cb:Map cb:String cb:String]]
  [documentation toolsRequired "The required tools"]
  [field toolsRequired [cb:List NPA1ToolReference]]
  [documentation toolExecution "The tool execution"]
  [field toolExecution NPA1ToolExecutionEvaluated]
  [documentation archiveLinks "The links to the archive used as input."]
  [field archiveLinks NPA1ArchiveLinks]
  [documentation lockResources "The set of resources that must be locked"]
  [field lockResources [cb:List cb:String]]
  [documentation failurePolicy "The failure policy"]
  [field failurePolicy NPA1FailurePolicy]
  [documentation cleanPolicy "The cleaning policy"]
  [field cleanPolicy NPA1CleanPolicy]
]

;
; Commands
;

[documentation NPA1CommandCLogin "A request to log in."]
[record NPA1CommandCLogin
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation key "The public key."]
  [field key NPA1PublicKey]
]

[documentation NPA1CommandCLoginComplete "A request to complete logging in."]
[record NPA1CommandCLoginComplete
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation completion "The challenge completion."]
  [field completion NPA1LoginChallengeCompletion]
]

[documentation NPA1CommandCEnvironmentInfo "A command to supply environment information."]
[record NPA1CommandCEnvironmentInfo
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation environmentVariables "The environment variables."]
  [field environmentVariables [cb:Map cb:String cb:String]]
  [documentation systemProperties "The system properties."]
  [field systemProperties [cb:Map cb:String cb:String]]
]

[documentation NPA1CommandCDisconnect "A request to disconnect."]
[record NPA1CommandCDisconnect
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPA1CommandCWorkItemStarted "A work item has started executing."]
[record NPA1CommandCWorkItemStarted
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation workItem "The work item identifier"]
  [field workItem NPA1WorkItemIdentifier]
]

[documentation NPA1CommandCWorkItemSucceeded "A work item finished executing successfully."]
[record NPA1CommandCWorkItemSucceeded
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation workItem "The work item identifier"]
  [field workItem NPA1WorkItemIdentifier]
]

[documentation NPA1CommandCWorkItemFailed "A work item finished executing with a failure."]
[record NPA1CommandCWorkItemFailed
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation workItem "The work item identifier"]
  [field workItem NPA1WorkItemIdentifier]
]

[documentation NPA1CommandCWorkItemOutput "A work item produced a line of output."]
[record NPA1CommandCWorkItemOutput
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation timestamp "The timestamp"]
  [field timestamp ct:OffsetDateTime]
  [documentation index "The log index"]
  [field index cb:IntegerUnsigned64]
  [documentation type "The event type"]
  [field type cb:String]
  [documentation workItem "The work item identifier"]
  [field workItem NPA1WorkItemIdentifier]
  [documentation attributes "The attributes"]
  [field attributes [cb:Map cb:String cb:String]]
  [documentation output "The line of output"]
  [field output cb:String]
  [documentation exception "The associated exception, if any."]
  [field exception [cb:Option NPA1Exception]]
]

[documentation NPA1CommandSLatencyCheck "A command to request latency information."]
[record NPA1CommandSLatencyCheck
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation timeCurrent "The current server time."]
  [field timeCurrent ct:OffsetDateTime]
]

[documentation NPA1CommandSWorkOffered "An offer of work to the client from the server."]
[record NPA1CommandSWorkOffered
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation workItem "The work item identifier"]
  [field workItem NPA1WorkItemIdentifier]
]

[documentation NPA1CommandSWorkSent "A sending of work to the client from the server."]
[record NPA1CommandSWorkSent
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation workItem "The work item"]
  [field workItem NPA1AgentWorkItem]
]

;
; Events
;

;
; Responses
;

[documentation NPA1ResponseLoginChallenge "A login challenge response."]
[record NPA1ResponseLoginChallenge
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation challenge "The challenge."]
  [field challenge NPA1LoginChallenge]
]

[documentation NPA1ResponseOK "A generic confirmation response."]
[record NPA1ResponseOK
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
]

[documentation NPA1ResponseError "An error response."]
[record NPA1ResponseError
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation errorCode "The error code."]
  [field errorCode cb:String]
  [documentation message "The error message."]
  [field message cb:String]
  [documentation attributes "The error attributes."]
  [field attributes [cb:Map cb:String cb:String]]
]

[documentation NPA1ResponseLatencyCheck "A response to a latency check."]
[record NPA1ResponseLatencyCheck
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation timeOriginal "The original server time."]
  [field timeOriginal ct:OffsetDateTime]
  [documentation timeClient "The current client time."]
  [field timeClient ct:OffsetDateTime]
]

[documentation NPA1ResponseWorkOffered "A response to a work offer."]
[record NPA1ResponseWorkOffered
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation workItem "The original work item."]
  [field workItem NPA1WorkItemIdentifier]
  [documentation accepted "Whether the work can be accepted."]
  [field accepted cb:Boolean]
]

[documentation NPA1ResponseWorkSent "A response to a work offer."]
[record NPA1ResponseWorkSent
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation workItem "The original work item."]
  [field workItem NPA1WorkItemIdentifier]
  [documentation accepted "Whether the work was accepted."]
  [field accepted cb:Boolean]
]

[documentation NPA "The Agent protocol."]
[protocol NPA
  [version 1
    [types-added
      NPA1CommandCDisconnect
      NPA1CommandCEnvironmentInfo
      NPA1CommandCLogin
      NPA1CommandCLoginComplete
      NPA1CommandCWorkItemFailed
      NPA1CommandCWorkItemOutput
      NPA1CommandCWorkItemStarted
      NPA1CommandCWorkItemSucceeded
      NPA1CommandSLatencyCheck
      NPA1CommandSWorkOffered
      NPA1CommandSWorkSent
      NPA1ResponseError
      NPA1ResponseLatencyCheck
      NPA1ResponseLoginChallenge
      NPA1ResponseOK
      NPA1ResponseWorkOffered
      NPA1ResponseWorkSent
    ]
  ]
]

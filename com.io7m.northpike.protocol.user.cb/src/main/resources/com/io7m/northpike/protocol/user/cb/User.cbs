;
; Copyright Â© 2023 Mark Raynsford <code@io7m.com> https://www.io7m.com
;
; Permission to use, copy, modify, and/or distribute this software for any
; purpose with or without fee is hereby granted, provided that the above
; copyright notice and this permission notice appear in all copies.
;
; THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
; WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
; MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY
; SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
; WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
; ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF OR
; IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
;

[language cedarbridge 1 0]

[package com.io7m.northpike.protocol.user.cb]

[import com.io7m.cedarbridge cb]
[import com.io7m.cedarbridge.time ct]

[documentation NPU1RepositoryCredentials "The credentials used to access a repository."]
[variant NPU1RepositoryCredentials
  [documentation None "No credentials required."]
  [case None]
  [documentation UsernamePassword "Credentials consisting of a username and password."]
  [case UsernamePassword
    [field username cb:String]
    [field password cb:String]
  ]
]

[documentation NPU1RepositorySigningPolicy "A repository signing policy."]
[variant NPU1RepositorySigningPolicy
  [case AllowUnsignedCommits]
  [case RequireCommitsSignedWithKnownKey]
  [case RequireCommitsSignedWithSpecificKeys]
]

[documentation NPU1RepositoryDescription "A description of a repository."]
[record NPU1RepositoryDescription
  [documentation provider "The SCM provider."]
  [field provider cb:String]
  [documentation id "The repository ID."]
  [field id cb:UUID]
  [documentation url "The repository URL."]
  [field url cb:String]
  [documentation credentials "The repository credentials."]
  [field credentials NPU1RepositoryCredentials]
  [documentation signingPolicy "The repository signing policy."]
  [field signingPolicy NPU1RepositorySigningPolicy]
]

[documentation NPU1RepositorySummary "A summary of a repository."]
[record NPU1RepositorySummary
  [documentation provider "The SCM provider."]
  [field provider cb:String]
  [documentation id "The repository ID."]
  [field id cb:UUID]
  [documentation url "The repository URL."]
  [field url cb:String]
]

[documentation NPU1Page "A page of search results."]
[record NPU1Page
  [parameter T]
  [field items [cb:List T]]
  [field pageIndex cb:IntegerUnsigned32]
  [field pageCount cb:IntegerUnsigned32]
  [field pageFirstOffset cb:IntegerUnsigned64]
]

[documentation NPU1AgentLabel "An agent label."]
[record NPU1AgentLabel
  [documentation name "The label name."]
  [field name cb:String]
  [documentation description "The label description."]
  [field description cb:String]
]

[documentation NPU1RepositorySearchParameters "The repository search parameters."]
[record NPU1RepositorySearchParameters
  [documentation pageSize "The page size."]
  [field pageSize cb:IntegerUnsigned32]
]

[documentation NPU1PlanSearchParameters "The plan search parameters."]
[record NPU1PlanSearchParameters
  [documentation query "The search query."]
  [field query cb:String]
  [documentation pageSize "The page size."]
  [field pageSize cb:IntegerUnsigned32]
]

[documentation NPU1AgentLabelSearchParameters "The agent label search parameters."]
[record NPU1AgentLabelSearchParameters
  [documentation query "The search query."]
  [field query cb:String]
  [documentation pageSize "The page size."]
  [field pageSize cb:IntegerUnsigned32]
]

[documentation NPU1ToolExecutionIdentifier "A tool execution identifier."]
[record NPU1ToolExecutionIdentifier
  [documentation name "The name."]
  [field name cb:String]
  [documentation version "The version."]
  [field version cb:IntegerUnsigned64]
]

[documentation NPU1ToolExecutionDescription "A tool execution description."]
[record NPU1ToolExecutionDescription
  [documentation identifier "The identifier."]
  [field identifier NPU1ToolExecutionIdentifier]
  [documentation toolName "The tool name."]
  [field toolName cb:String]
  [documentation description "The humanly-readable description."]
  [field description cb:String]
  [documentation formatName "The format name."]
  [field formatName cb:String]
  [documentation text "The execution text."]
  [field text cb:String]
]

[documentation NPU1ToolExecutionDescriptionSummary "A tool execution description summary."]
[record NPU1ToolExecutionDescriptionSummary
  [documentation identifier "The identifier."]
  [field identifier NPU1ToolExecutionIdentifier]
  [documentation toolName "The tool name."]
  [field toolName cb:String]
  [documentation description "The humanly-readable description."]
  [field description cb:String]
]

[documentation NPU1CompilationMessage "A compilation message."]
[record NPU1CompilationMessage
  [documentation kind "The message kind."]
  [field kind cb:String]
  [documentation line "The message line."]
  [field line cb:IntegerUnsigned32]
  [documentation column "The message column."]
  [field column cb:IntegerUnsigned32]
  [documentation message "The message text."]
  [field message cb:String]
]

[documentation NPU1ToolExecutionVariable "A tool execution variable."]
[variant NPU1ToolExecutionVariable
  [case Integer
    [field name  cb:String]
    [field value cb:IntegerSigned64]
  ]
  [case Boolean
    [field name  cb:String]
    [field value cb:Boolean]
  ]
  [case String
    [field name  cb:String]
    [field value cb:String]
  ]
  [case StringSet
    [field name  cb:String]
    [field value [cb:List cb:String]]
  ]
]

[documentation NPU1ToolExecutionDescriptionSearchParameters "The tool execution description search parameters."]
[record NPU1ToolExecutionDescriptionSearchParameters
  [documentation toolName "Restrict searching to executions compatible with this tool."]
  [field toolName [cb:Option cb:String]]
  [documentation pageSize "The page size."]
  [field pageSize cb:IntegerUnsigned32]
]

[documentation NPU1PlanIdentifier "A plan identifier."]
[record NPU1PlanIdentifier
  [documentation name "The name."]
  [field name cb:String]
  [documentation version "The version."]
  [field version cb:IntegerUnsigned64]
]

[documentation NPU1PlanDescription "A plan description."]
[record NPU1PlanDescription
  [documentation identifier "The identifier."]
  [field identifier NPU1PlanIdentifier]
  [documentation formatName "The format name."]
  [field formatName cb:String]
  [documentation text "The execution text."]
  [field text cb:String]
]

[documentation NPU1PlanSummary "A plan summary."]
[record NPU1PlanSummary
  [documentation identifier "The identifier."]
  [field identifier NPU1PlanIdentifier]
  [documentation description "The plan description."]
  [field description cb:String]
]

[documentation NPU1AgentSummary "A summary of an agent."]
[record NPU1AgentSummary
  [documentation id "The agent ID."]
  [field id cb:UUID]
  [documentation name "The agent name."]
  [field name cb:String]
]

[documentation NPU1AgentDescription "A description of an agent."]
[record NPU1AgentDescription
  [documentation id "The agent ID."]
  [field id cb:UUID]
  [documentation name "The agent name."]
  [field name cb:String]
  [documentation accessKey "The agent access key."]
  [field accessKey cb:String]
  [documentation environmentVariables "The agent environment."]
  [field environmentVariables [cb:Map cb:String cb:String]]
  [documentation systemProperties "The agent system properties."]
  [field systemProperties [cb:Map cb:String cb:String]]
  [documentation labels "The agent labels."]
  [field labels [cb:Map cb:String NPU1AgentLabel]]
]

[documentation NPU1AgentLabelMatch "A label match expression."]
[variant NPU1AgentLabelMatch
  [case AnyLabel]
  [case And
    [field e0 NPU1AgentLabelMatch]
    [field e1 NPU1AgentLabelMatch]
  ]
  [case Or
    [field e0 NPU1AgentLabelMatch]
    [field e1 NPU1AgentLabelMatch]
  ]
  [case Specific
    [field name cb:String]
  ]
]

[documentation NPU1AgentSearchParameters "The agent search parameters."]
[record NPU1AgentSearchParameters
  [documentation matchLabels "The expression against which to match agent labels."]
  [field matchLabels NPU1AgentLabelMatch]
  [documentation pageSize "The page size."]
  [field pageSize cb:IntegerUnsigned32]
]

[documentation NPU1AssignmentSearchParameters "The assignment search parameters."]
[record NPU1AssignmentSearchParameters
  [documentation repositoryId "Only include assignments with this repository ID."]
  [field repositoryId [cb:Option cb:UUID]]
  [documentation plan "Only include assignments with this plan."]
  [field plan [cb:Option NPU1PlanIdentifier]]
  [documentation nameQuery "Only include assignments with names matching this expression."]
  [field nameQuery NPU1NameMatch]
  [documentation pageSize "The page size."]
  [field pageSize cb:IntegerUnsigned32]
]

[documentation NPU1Assignment "A description of an assignment."]
[record NPU1Assignment
  [documentation name "The assignment name."]
  [field name cb:String]
  [documentation repository "The repository ID."]
  [field repository cb:UUID]
  [documentation plan "The plan ID."]
  [field plan NPU1PlanIdentifier]
]

[documentation NPU1NameMatch "A name match expression."]
[variant NPU1NameMatch
  [case AnyName]
  [case Exact [field name cb:String]]
  [case Similar [field name cb:String]]
]

[documentation NPU1AssignmentExecutionRequest "A request to start an assignment execution."]
[record NPU1AssignmentExecutionRequest
  [documentation name "The assignment name."]
  [field name cb:String]
  [documentation commit "The commit."]
  [field commit cb:String]
]

[documentation NPU1AssignmentExecution "A description of an assignment execution."]
[record NPU1AssignmentExecution
  [documentation id "The execution ID."]
  [field id cb:UUID]
  [documentation assignment "The assignment."]
  [field assignment NPU1Assignment]
  [documentation commit "The commit."]
  [field commit cb:String]
]

[documentation NPU1AssignmentExecutionState "The state of an execution."]
[variant NPU1AssignmentExecutionState
  [case Requested
    [documentation id "The execution ID."]
    [field id cb:UUID]
    [documentation request "The request that started the execution."]
    [field request NPU1AssignmentExecutionRequest]
    [field timeCreated ct:OffsetDateTime]
  ]
  [case Created
    [documentation id "The execution ID."]
    [field id cb:UUID]
    [field timeCreated ct:OffsetDateTime]
    [field execution NPU1AssignmentExecution]
  ]
  [case Cancelled
    [documentation id "The execution ID."]
    [field id cb:UUID]
    [documentation request "The request that started the execution."]
    [field request NPU1AssignmentExecutionRequest]
    [field timeCreated ct:OffsetDateTime]
    [field timeStarted ct:OffsetDateTime]
    [field timeEnded ct:OffsetDateTime]
  ]
  [case CreationFailed
    [documentation id "The execution ID."]
    [field id cb:UUID]
    [documentation request "The request that started the execution."]
    [field request NPU1AssignmentExecutionRequest]
    [field timeCreated ct:OffsetDateTime]
  ]
  [case Running
    [documentation execution "The assignment execution."]
    [field execution NPU1AssignmentExecution]
    [field timeCreated ct:OffsetDateTime]
    [field timeStarted ct:OffsetDateTime]
  ]
  [case Failed
    [documentation execution "The assignment execution."]
    [field execution NPU1AssignmentExecution]
    [field timeCreated ct:OffsetDateTime]
    [field timeStarted ct:OffsetDateTime]
    [field timeEnded ct:OffsetDateTime]
  ]
  [case Succeeded
    [documentation execution "The assignment execution."]
    [field execution NPU1AssignmentExecution]
    [field timeCreated ct:OffsetDateTime]
    [field timeStarted ct:OffsetDateTime]
    [field timeEnded ct:OffsetDateTime]
  ]
]

[documentation NPU1AssignmentExecutionSearchParameters "The assignment execution search parameters."]
[record NPU1AssignmentExecutionSearchParameters
  [documentation repositoryId "Only include assignments with this repository ID."]
  [field repositoryId [cb:Option cb:UUID]]
  [documentation plan "Only include assignments with this plan."]
  [field plan [cb:Option NPU1PlanIdentifier]]
  [documentation nameQuery "Only include assignments with names matching this expression."]
  [field nameQuery NPU1NameMatch]
  [documentation pageSize "The page size."]
  [field pageSize cb:IntegerUnsigned32]
]

[documentation NPU1PublicKeySearchParameters "The public key search parameters."]
[record NPU1PublicKeySearchParameters
  [documentation pageSize "The page size."]
  [field pageSize cb:IntegerUnsigned32]
]

[documentation NPU1WorkItemStatus "The status of a work item."]
[variant NPU1WorkItemStatus
  [case Created]
  [case Accepted]
  [case Running]
  [case Succeeded]
  [case Failed]
]

[documentation NPU1WorkItemIdentifier "A work item identifier."]
[record NPU1WorkItemIdentifier
  [documentation execution "The assignment execution."]
  [field execution cb:UUID]
  [documentation planElement "The plan element name."]
  [field planElement cb:String]
]

[documentation NPU1WorkItem "A work item within an assignment execution."]
[record NPU1WorkItem
  [documentation identifier "A work item identifier."]
  [field identifier NPU1WorkItemIdentifier]
  [documentation selectedAgent "The agent selected, if any."]
  [field selectedAgent [cb:Option cb:UUID]]
  [documentation status "The work item status."]
  [field status NPU1WorkItemStatus]
]

[documentation NPU1UserOrAgentID "A user ID or agent ID."]
[variant NPU1UserOrAgentID
  [case User  [field id cb:UUID]]
  [case Agent [field id cb:UUID]]
]

[documentation NPU1TimeRange "An inclusive time range."]
[record NPU1TimeRange
  [field timeLower ct:OffsetDateTime]
  [field timeUpper ct:OffsetDateTime]
]

[documentation NPU1AuditSearchParameters "The audit search parameters."]
[record NPU1AuditSearchParameters
  [documentation owner "Limit to events with the given owner."]
  [field owner [cb:Option NPU1UserOrAgentID]]
  [documentation type "Limit to events with the given type."]
  [field type [cb:Option cb:String]]
  [documentation timeRange "Limit to events in the given time range."]
  [field timeRange NPU1TimeRange]
  [documentation pageSize "The page size."]
  [field pageSize cb:IntegerUnsigned32]
]

[documentation NPU1AuditEvent "An audit event."]
[record NPU1AuditEvent
  [documentation time "The event time."]
  [field time ct:OffsetDateTime]
  [documentation owner "The event owner."]
  [field owner NPU1UserOrAgentID]
  [documentation type "The event type."]
  [field type cb:String]
  [documentation data "The event data."]
  [field data [cb:Map cb:String cb:String]]
]

;
; Commands
;

[documentation NPU1CommandLogin "A request to log in."]
[record NPU1CommandLogin
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation userName "The user name"]
  [field userName cb:String]
  [documentation password "The password"]
  [field password cb:String]
]

[documentation NPU1CommandDisconnect "A request to disconnect."]
[record NPU1CommandDisconnect
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

;
; Roles
;

[documentation NPU1CommandRolesAssign "Assign a set of roles to a user."]
[record NPU1CommandRolesAssign
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation user "The target user."]
  [field user cb:UUID]
  [documentation roles "The assigned roles."]
  [field roles [cb:List cb:String]]
]

[documentation NPU1CommandRolesRevoke "Revoke a set of roles from a user."]
[record NPU1CommandRolesRevoke
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation user "The target user."]
  [field user cb:UUID]
  [documentation roles "The roles."]
  [field roles [cb:List cb:String]]
]

[documentation NPU1CommandRolesRevoke "Get the roles for a user."]
[record NPU1CommandRolesGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation user "The target user."]
  [field user cb:UUID]
]

;
; Repositories
;

[documentation NPU1CommandRepositoryPut "Create or update a repository."]
[record NPU1CommandRepositoryPut
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation repository "The repository"]
  [field repository NPU1RepositoryDescription]
]

[documentation NPU1CommandRepositoryGet "Get a repository."]
[record NPU1CommandRepositoryGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation repository "The repository ID"]
  [field repository cb:UUID]
]

[documentation NPU1CommandRepositorySearchBegin "Start searching repositories."]
[record NPU1CommandRepositorySearchBegin
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation parameters "The search parameters."]
  [field parameters NPU1RepositorySearchParameters]
]

[documentation NPU1CommandRepositorySearchNext "Continue searching repositories."]
[record NPU1CommandRepositorySearchNext
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandRepositorySearchPrevious "Continue searching repositories."]
[record NPU1CommandRepositorySearchPrevious
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandRepositoryPublicKeyAssign "Assign a public key to a repository."]
[record NPU1CommandRepositoryPublicKeyAssign
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation repository "The repository ID"]
  [field repository cb:UUID]
  [documentation publicKey "The public key"]
  [field publicKey cb:String]
]

[documentation NPU1CommandRepositoryPublicKeyUnassign "Unassign a public key from a repository."]
[record NPU1CommandRepositoryPublicKeyUnassign
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation repository "The repository ID"]
  [field repository cb:UUID]
  [documentation publicKey "The public key"]
  [field publicKey cb:String]
]

[documentation NPU1CommandRepositoryPublicKeysAssigned "Retrieve the set of keys assigned to a repository."]
[record NPU1CommandRepositoryPublicKeysAssigned
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation repository "The repository ID"]
  [field repository cb:UUID]
]

;
; Agent Labels
;

[documentation NPU1CommandAgentLabelPut "Create or update an agent label."]
[record NPU1CommandAgentLabelPut
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation label "The label"]
  [field label NPU1AgentLabel]
]

[documentation NPU1CommandAgentLabelGet "Get an agent label."]
[record NPU1CommandAgentLabelGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation name "The label name"]
  [field name cb:String]
]

[documentation NPU1CommandAgentLabelSearchBegin "Start searching labels."]
[record NPU1CommandAgentLabelSearchBegin
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation parameters "The search parameters."]
  [field parameters NPU1AgentLabelSearchParameters]
]

[documentation NPU1CommandAgentLabelSearchNext "Continue searching labels."]
[record NPU1CommandAgentLabelSearchNext
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandAgentLabelSearchPrevious "Continue searching labels."]
[record NPU1CommandAgentLabelSearchPrevious
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandAgentLabelDelete "Delete agent labels."]
[record NPU1CommandAgentLabelDelete
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation names "The label names"]
  [field names [cb:List cb:String]]
]

;
; Tool executions
;

[documentation NPU1CommandToolExecutionDescriptionPut "Create or update a tool execution description."]
[record NPU1CommandToolExecutionDescriptionPut
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation description "The description"]
  [field description NPU1ToolExecutionDescription]
]

[documentation NPU1CommandToolExecutionDescriptionGet "Get a tool execution description."]
[record NPU1CommandToolExecutionDescriptionGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation identifier "The identifier"]
  [field identifier NPU1ToolExecutionIdentifier]
]

[documentation NPU1CommandToolExecutionDescriptionValidate "Validate a tool execution description."]
[record NPU1CommandToolExecutionDescriptionValidate
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation variables "The variables."]
  [field variables [cb:List NPU1ToolExecutionVariable]]
  [documentation description "The description"]
  [field description NPU1ToolExecutionDescription]
]

[documentation NPU1CommandToolExecutionDescriptionSearchBegin "Start searching tool execution descriptions."]
[record NPU1CommandToolExecutionDescriptionSearchBegin
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation parameters "The search parameters."]
  [field parameters NPU1ToolExecutionDescriptionSearchParameters]
]

[documentation NPU1CommandToolExecutionDescriptionSearchNext "Continue searching tool execution descriptions."]
[record NPU1CommandToolExecutionDescriptionSearchNext
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandToolExecutionDescriptionSearchPrevious "Continue searching tool execution descriptions."]
[record NPU1CommandToolExecutionDescriptionSearchPrevious
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

;
; Plans
;

[documentation NPU1CommandPlanPut "Create or update a plan."]
[record NPU1CommandPlanPut
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation plan "The plan"]
  [field plan NPU1PlanDescription]
]

[documentation NPU1CommandPlanGet "Get a plan."]
[record NPU1CommandPlanGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation plan "The plan ID"]
  [field plan NPU1PlanIdentifier]
]

[documentation NPU1CommandPlanValidate "Validate a plan description."]
[record NPU1CommandPlanValidate
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation description "The description"]
  [field description NPU1PlanDescription]
]

[documentation NPU1CommandPlanSearchBegin "Start searching plans."]
[record NPU1CommandPlanSearchBegin
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation parameters "The search parameters."]
  [field parameters NPU1PlanSearchParameters]
]

[documentation NPU1CommandPlanSearchNext "Continue searching plans."]
[record NPU1CommandPlanSearchNext
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandPlanSearchPrevious "Continue searching plans."]
[record NPU1CommandPlanSearchPrevious
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

;
; Agents
;

[documentation NPU1CommandAgentPut "Create or update an agent."]
[record NPU1CommandAgentPut
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation agent "The agent"]
  [field agent NPU1AgentDescription]
]

[documentation NPU1CommandAgentGet "Get an agent."]
[record NPU1CommandAgentGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation agent "The agent ID"]
  [field agent cb:UUID]
]

[documentation NPU1CommandAgentSearchBegin "Start searching agents."]
[record NPU1CommandAgentSearchBegin
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation parameters "The search parameters."]
  [field parameters NPU1AgentSearchParameters]
]

[documentation NPU1CommandAgentSearchNext "Continue searching agents."]
[record NPU1CommandAgentSearchNext
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandAgentSearchPrevious "Continue searching agents."]
[record NPU1CommandAgentSearchPrevious
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

;
; Assignments
;

[documentation NPU1CommandAssignmentPut "Create or update a assignment."]
[record NPU1CommandAssignmentPut
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation assignment "The assignment"]
  [field assignment NPU1Assignment]
]

[documentation NPU1CommandAssignmentGet "Get a assignment."]
[record NPU1CommandAssignmentGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation assignment "The assignment name"]
  [field assignment cb:String]
]

[documentation NPU1CommandAssignmentSearchBegin "Start searching assignments."]
[record NPU1CommandAssignmentSearchBegin
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation parameters "The search parameters."]
  [field parameters NPU1AssignmentSearchParameters]
]

[documentation NPU1CommandAssignmentSearchNext "Continue searching assignments."]
[record NPU1CommandAssignmentSearchNext
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandAssignmentSearchPrevious "Continue searching assignments."]
[record NPU1CommandAssignmentSearchPrevious
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandAssignmentExecute "Request the execution of an assignment."]
[record NPU1CommandAssignmentExecute
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation assignment "The assignment name."]
  [field assignment cb:String]
  [documentation commitId "The commit ID."]
  [field commitId cb:String]
]

[documentation NPU1CommandAssignmentExecutionSearchBegin "Start searching assignment executions."]
[record NPU1CommandAssignmentExecutionSearchBegin
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation parameters "The search parameters."]
  [field parameters NPU1AssignmentExecutionSearchParameters]
]

[documentation NPU1CommandAssignmentExecutionSearchNext "Continue searching assignment executions."]
[record NPU1CommandAssignmentExecutionSearchNext
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandAssignmentExecutionSearchPrevious "Continue searching assignment executions."]
[record NPU1CommandAssignmentExecutionSearchPrevious
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandAssignmentExecutionWorkItems "Get the work items for the given execution."]
[record NPU1CommandAssignmentExecutionWorkItems
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation execution "The assignment execution."]
  [field execution cb:UUID]
]

;
; Public Keys
;

[documentation NPU1CommandPublicKeyPut "Create or update a public key."]
[record NPU1CommandPublicKeyPut
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation publicKey "The public key"]
  [field publicKey cb:String]
]

[documentation NPU1CommandPublicKeyGet "Get a public key."]
[record NPU1CommandPublicKeyGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation fingerprint "The public key fingerprint"]
  [field fingerprint cb:String]
]

[documentation NPU1CommandPublicKeySearchBegin "Start searching public keys."]
[record NPU1CommandPublicKeySearchBegin
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation parameters "The search parameters."]
  [field parameters NPU1PublicKeySearchParameters]
]

[documentation NPU1CommandPublicKeySearchNext "Continue searching public keys."]
[record NPU1CommandPublicKeySearchNext
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandPublicKeySearchPrevious "Continue searching public keys."]
[record NPU1CommandPublicKeySearchPrevious
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandPublicKeyDelete "Delete a public key."]
[record NPU1CommandPublicKeyDelete
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation fingerprint "The public key fingerprint"]
  [field fingerprint cb:String]
]

;
; Audit
;

[documentation NPU1CommandAuditSearchBegin "Start searching audit events."]
[record NPU1CommandAuditSearchBegin
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation parameters "The search parameters."]
  [field parameters NPU1AuditSearchParameters]
]

[documentation NPU1CommandAuditSearchNext "Continue searching audit events."]
[record NPU1CommandAuditSearchNext
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

[documentation NPU1CommandAuditSearchPrevious "Continue searching audit events."]
[record NPU1CommandAuditSearchPrevious
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
]

;
; Events
;

;
; Responses
;

[documentation NPU1ResponseOK "A generic confirmation response."]
[record NPU1ResponseOK
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
]

[documentation NPU1ResponseError "An error response."]
[record NPU1ResponseError
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation errorCode "The error code."]
  [field errorCode cb:String]
  [documentation message "The error message."]
  [field message cb:String]
  [documentation attributes "The error attributes."]
  [field attributes [cb:Map cb:String cb:String]]
]

[documentation NPU1ResponseRepositoryGet "A repository retrieval."]
[record NPU1ResponseRepositoryGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation repository "The repository description"]
  [field repository [cb:Option NPU1RepositoryDescription]]
]

[documentation NPU1ResponseRepositoryPublicKeysAssigned "A retrieval of repository key assignments."]
[record NPU1ResponseRepositoryPublicKeysAssigned
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation keys "The repository keys"]
  [field keys [cb:List cb:String]]
]

[documentation NPU1ResponseRolesGet "A response to NPU1ResponseRolesGet."]
[record NPU1ResponseRolesGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation roles "The roles."]
  [field roles [cb:List cb:String]]
]

[documentation NPU1ResponseRepositorySearch "A response to NPU1CommandRepositorySearchBegin."]
[record NPU1ResponseRepositorySearch
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation results "The results"]
  [field results [NPU1Page NPU1RepositorySummary]]
]

[documentation NPU1ResponseAgentLabelGet "A label retrieval."]
[record NPU1ResponseAgentLabelGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation label "The label description"]
  [field label [cb:Option NPU1AgentLabel]]
]

[documentation NPU1ResponseAgentLabelSearch "A response to NPU1CommandAgentLabelSearchBegin."]
[record NPU1ResponseAgentLabelSearch
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation results "The results"]
  [field results [NPU1Page NPU1AgentLabel]]
]

[documentation NPU1ResponseToolExecutionDescriptionValidate "A response to NPU1CommandToolExecutionDescriptionValidate."]
[record NPU1ResponseToolExecutionDescriptionValidate
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation compilationMessages "The compilation messages."]
  [field compilationMessages [cb:List NPU1CompilationMessage]]
]

[documentation NPU1ResponseToolExecutionDescriptionGet "A response to NPU1CommandToolExecutionDescriptionGet."]
[record NPU1ResponseToolExecutionDescriptionGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation description "The description."]
  [field description [cb:Option NPU1ToolExecutionDescription]]
]

[documentation NPU1ResponseToolExecutionDescriptionSearch "A response to NPU1CommandToolExecutionDescriptionSearchBegin."]
[record NPU1ResponseToolExecutionDescriptionSearch
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation results "The results"]
  [field results [NPU1Page NPU1ToolExecutionDescriptionSummary]]
]

[documentation NPU1ResponsePlanValidate "A response to NPU1CommandPlanValidate."]
[record NPU1ResponsePlanValidate
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation compilationMessages "The compilation messages."]
  [field compilationMessages [cb:List NPU1CompilationMessage]]
]

[documentation NPU1ResponsePlanGet "A response to NPU1CommandPlanGet."]
[record NPU1ResponsePlanGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation description "The description."]
  [field description [cb:Option NPU1PlanDescription]]
]

[documentation NPU1ResponsePlanSearch "A response to NPU1CommandPlanSearchBegin."]
[record NPU1ResponsePlanSearch
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation results "The results"]
  [field results [NPU1Page NPU1PlanSummary]]
]

[documentation NPU1ResponseAgentGet "A response to NPU1CommandAgentGet."]
[record NPU1ResponseAgentGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation description "The description."]
  [field description [cb:Option NPU1AgentDescription]]
]

[documentation NPU1ResponseAgentSearch "A response to NPU1CommandAgentSearchBegin."]
[record NPU1ResponseAgentSearch
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation results "The results"]
  [field results [NPU1Page NPU1AgentSummary]]
]

[documentation NPU1ResponseAssignmentGet "A response to NPU1CommandAssignmentGet."]
[record NPU1ResponseAssignmentGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation description "The description."]
  [field description [cb:Option NPU1Assignment]]
]

[documentation NPU1ResponseAssignmentSearch "A response to NPU1CommandAssignmentSearchBegin."]
[record NPU1ResponseAssignmentSearch
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation results "The results"]
  [field results [NPU1Page NPU1Assignment]]
]

[documentation NPU1ResponseAssignmentExecutionSearch "A response to NPU1CommandAssignmentExecutionSearchBegin."]
[record NPU1ResponseAssignmentExecutionSearch
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation results "The results"]
  [field results [NPU1Page NPU1AssignmentExecutionState]]
]

[documentation NPU1ResponseAssignmentExecutionWorkItems "A response to NPU1CommandAssignmentExecutionWorkItems."]
[record NPU1ResponseAssignmentExecutionWorkItems
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation results "The results"]
  [field results [cb:List NPU1WorkItem]]
]

[documentation NPU1ResponsePublicKeyGet "A response to NPU1CommandPublicKeyGet."]
[record NPU1ResponsePublicKeyGet
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation publicKey "The public key."]
  [field publicKey [cb:Option cb:String]]
]

[documentation NPU1ResponsePublicKeySearch "A response to NPU1CommandPublicKeySearchBegin."]
[record NPU1ResponsePublicKeySearch
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation results "The results"]
  [field results [NPU1Page cb:String]]
]

[documentation NPU1ResponseAuditSearch "A response to NPU1CommandAuditSearchBegin."]
[record NPU1ResponseAuditSearch
  [documentation messageId "The message ID"]
  [field messageId cb:UUID]
  [documentation correlationId "The message correlation ID"]
  [field correlationId cb:UUID]
  [documentation results "The results"]
  [field results [NPU1Page NPU1AuditEvent]]
]

[documentation NPU "The User protocol."]
[protocol NPU
  [version 1
    [types-added
      NPU1CommandAgentGet
      NPU1CommandAgentLabelDelete
      NPU1CommandAgentLabelGet
      NPU1CommandAgentLabelPut
      NPU1CommandAgentLabelSearchBegin
      NPU1CommandAgentLabelSearchNext
      NPU1CommandAgentLabelSearchPrevious
      NPU1CommandAgentPut
      NPU1CommandAgentSearchBegin
      NPU1CommandAgentSearchNext
      NPU1CommandAgentSearchPrevious
      NPU1CommandAssignmentExecute
      NPU1CommandAssignmentExecutionSearchBegin
      NPU1CommandAssignmentExecutionSearchNext
      NPU1CommandAssignmentExecutionSearchPrevious
      NPU1CommandAssignmentExecutionWorkItems
      NPU1CommandAssignmentGet
      NPU1CommandAssignmentPut
      NPU1CommandAssignmentSearchBegin
      NPU1CommandAssignmentSearchNext
      NPU1CommandAssignmentSearchPrevious
      NPU1CommandAuditSearchBegin
      NPU1CommandAuditSearchNext
      NPU1CommandAuditSearchPrevious
      NPU1CommandDisconnect
      NPU1CommandLogin
      NPU1CommandPlanGet
      NPU1CommandPlanPut
      NPU1CommandPlanSearchBegin
      NPU1CommandPlanSearchNext
      NPU1CommandPlanSearchPrevious
      NPU1CommandPlanValidate
      NPU1CommandPublicKeyDelete
      NPU1CommandPublicKeyGet
      NPU1CommandPublicKeyPut
      NPU1CommandPublicKeySearchBegin
      NPU1CommandPublicKeySearchNext
      NPU1CommandPublicKeySearchPrevious
      NPU1CommandRepositoryGet
      NPU1CommandRepositoryPublicKeyAssign
      NPU1CommandRepositoryPublicKeyUnassign
      NPU1CommandRepositoryPublicKeysAssigned
      NPU1CommandRepositoryPut
      NPU1CommandRepositorySearchBegin
      NPU1CommandRepositorySearchNext
      NPU1CommandRepositorySearchPrevious
      NPU1CommandRolesAssign
      NPU1CommandRolesGet
      NPU1CommandRolesRevoke
      NPU1CommandToolExecutionDescriptionGet
      NPU1CommandToolExecutionDescriptionPut
      NPU1CommandToolExecutionDescriptionSearchBegin
      NPU1CommandToolExecutionDescriptionSearchNext
      NPU1CommandToolExecutionDescriptionSearchPrevious
      NPU1CommandToolExecutionDescriptionValidate
      NPU1ResponseAgentGet
      NPU1ResponseAgentLabelGet
      NPU1ResponseAgentLabelSearch
      NPU1ResponseAgentSearch
      NPU1ResponseAssignmentExecutionSearch
      NPU1ResponseAssignmentExecutionWorkItems
      NPU1ResponseAssignmentGet
      NPU1ResponseAssignmentSearch
      NPU1ResponseAuditSearch
      NPU1ResponseError
      NPU1ResponseOK
      NPU1ResponsePlanGet
      NPU1ResponsePlanSearch
      NPU1ResponsePlanValidate
      NPU1ResponsePublicKeyGet
      NPU1ResponsePublicKeySearch
      NPU1ResponseRepositoryGet
      NPU1ResponseRepositoryPublicKeysAssigned
      NPU1ResponseRepositorySearch
      NPU1ResponseRolesGet
      NPU1ResponseToolExecutionDescriptionGet
      NPU1ResponseToolExecutionDescriptionSearch
      NPU1ResponseToolExecutionDescriptionValidate
    ]
  ]
]
